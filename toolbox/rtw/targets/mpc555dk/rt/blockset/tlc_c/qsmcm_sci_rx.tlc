%% File: qsmcm_sci_rx.tlc
%%
%% Abstract:
%%   Receive data over the MPC555 Serial Communications Interface
%%
%% $Revision: 1.2 $
%% $Date: 2002/12/10 16:49:04 $
%%
%% Copyright 2002 The MathWorks, Inc.

%implements qsmcm_sci_rx "C"
%include "mpc555_common_includes.tlc"

%% Function: BlockTypeSetup
%% Abstract:
%%
%function BlockTypeSetup(block,system) void

  %if !SLibCodeGenForSim() 

     %% Only generate code for real time not for PIL
		
     %openfile buffer
     #include "qsmcm_sci.h"
     %closefile buffer
     %<LibCacheIncludes(buffer)> 
     
   %endif
   
%endfunction %% BlockTypeSetup

%% Function: Outputs ========================================================
%% Abstract:
%%
%function Outputs(block,system) Output

    %if !SLibCodeGenForSim() 
      
      %% Only generate code for real time not for PIL
      %assign module = "sci%<CAST("Number",SFcnParamSettings.Module)>"
      %assign data_addr = LibBlockOutputSignalAddr(0,"","",0)
      %assign inIdx = 0
      %if SFcnParamSettings.ShowLength
	%assign length =  "%<LibBlockInputSignal(inIdx,"","",0)>"
	%assign inIdx = inIdx + 1
      %else
	%assign length = LibBlockOutputSignalWidth(0)
      %endif
      
      %assign outIdx = 1
      %if SFcnParamSettings.ShowNumSent
	%assign numBytesRead = "%<LibBlockOutputSignal(outIdx,"","",0)> = "
	%assign outIdx = outIdx + 1
      %else
	%assign numBytesRead = ""
      %endif
      
      %if SFcnParamSettings.ShowReset == 1
	if (%<LibBlockInputSignal(inIdx,"","",0)>) {
	  qsmcm_%<module>_reset();
	} else {
	%endif	  
	{
	  %assign maxLen = CAST("Unsigned",SFcnParamSettings.MaxLength)
	  uint32_T tmpVar = (%<length> <= %<maxLen>) ? %<length> : %<maxLen>;
	  %<numBytesRead> \
	  general_get_string_%<module>( %<data_addr>,tmpVar);
	}
	%if SFcnParamSettings.ShowReset == 1
	}
      %endif
      
      %if SFcnParamSettings.ShowFlags == 1
	%if module=="sci1"
	  %<LibBlockOutputSignal(outIdx,"","",0)> = ( ( mpc555_qsmcm_sci_flags & MPC555_QSMCM_SCI1_PE_FE ) != 0 );
	%elseif module=="sci2"
	  %<LibBlockOutputSignal(outIdx,"","",0)> = ( ( mpc555_qsmcm_sci_flags & MPC555_QSMCM_SCI2_PE_FE ) != 0 );
	%else
	  %exit "Unrecognized value %<module> for module"
	%endif
	%assign outIdx = outIdx + 1
      %endif
      %if SFcnParamSettings.ShowOverrun == 1
	%if module=="sci1"
	  %<LibBlockOutputSignal(outIdx,"","",0)> = QSMCM.QSCI1SR.B.QOR;
	%elseif module=="sci2"
	  %<LibBlockOutputSignal(outIdx,"","",0)> = (( mpc555_qsmcm_sci_flags & MPC555_QSMCM_SCI2_OR ) != 0); 
	%else
	  %exit "Unrecognized value %<module> for module"
	%endif
	%assign outIdx = outIdx + 1
      %endif
      
    %endif
      
%endfunction %%Outputs




