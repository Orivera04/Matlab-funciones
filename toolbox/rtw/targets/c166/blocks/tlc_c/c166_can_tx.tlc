%% File: c166_can_tx.tlc
%%
%% Abstract:
%%   CAN transmit block
%%
%% $Revision: 1.1.6.4 $
%% $Date: 2004/04/19 01:17:44 $
%%
%% Copyright 2002-2003 The MathWorks, Inc.

%implements c166_can_tx "C"
%include "c166_common_includes.tlc"
%include "can_comm_setup.tlc"

%% Function: Start =============================================
%% Abstract:
%%
%function Start(block,system) Output

  %assign twinCAN = FEVAL("c166cache","get","TwinCAN")
   %if  ( !SLibCodeGenForSim() ) && ( twinCAN == 0 )
     %% Only generate code for real time
     
     %if SFcnParamSettings.TxMode==3
       
       %assign module = SFcnParamSettings.Module
       
       %switch module
	 %case 1
	   %assign moduleLetter = "A"
	   %assign moduleNo = 0
	   %break
	 %case 2
	   %assign moduleLetter = "B"
	   %assign moduleNo = 1
	   %break
	 %default
	   %exit "Invalid value for Module, Module = %<Module>"
       %endswitch
       
       %assign bufferIndex=SFcnParamSettings.Buffers[0]-1

       %assign qlen = SFcnParamSettings.FifoQueueLength
	/* Initialize CAN module %<moduleLetter>, buffer %<bufferIndex> for operation with FIFO 
	* queue of length %<qlen>; the queue is used to buffer messages to be transmitted.
	 */
	 { 
	 static MESSAGE_CIRCULAR_BUFFER msgBuf;
	 static CAN_FRAME can_frames[%<qlen>];
	 %<LibBlockPWork(pCanTxCircBuffer,"","",0)> = &msgBuf;
	 can_%<module>_init_transmit_fifo(%<bufferIndex>,&msgBuf, can_frames, %<qlen>);
	 }
       %endif
   %endif

%endfunction %% Start



%% Function: BlockTypeSetup
%% Abstract:
%%
%function BlockTypeSetup(block,system) void
   %<SetupCanDataTypes(block)>

   %if !SLibCodeGenForSim() 

     %% Only generate code for real time
     
     %openfile buffer
     #include <string.h>
     %closefile buffer
     
   %endif
   
%endfunction %% BlockTypeSetup

%% Function: Outputs ========================================================
%% Abstract:
%%
%function Outputs(block,system) Output

  %assign twinCAN = FEVAL("c166cache","get","TwinCAN")
  %if ( !SLibCodeGenForSim() ) && ( twinCAN == 0 )

    %switch SFcnParamSettings.Module
      %case 1
	%assign moduleLetter = "A"
	%assign moduleNo = 0
	%break
      %case 2
	%assign moduleLetter = "B"
	%assign moduleNo = 1
	%break
      %default
	%exit "Invalid value for Module, Module = %<Module>"
    %endswitch
        
    %assign module = SFcnParamSettings.Module
    
    %assign rollVars = ["U"]
    %roll idx = RollRegions, lcv = RollThreshold, block, "Roller", rollVars
      %if SFcnParamSettings.TxMode==1
	/* Send message using shared buffer */
	can_%<module>_send_msg(%<LibBlockInputSignalAddr(0, "", lcv, idx)>);
      %elseif SFcnParamSettings.TxMode==2
	%assign bufferIndex=SFcnParamSettings.Buffers[idx] - 1
	/* Send message using dedicated buffer */
	can_%<module>_load_msg(%<LibBlockInputSignalAddr(0, "", lcv, idx)>, \
	%<CAST("Unsigned", bufferIndex)> );
      %elseif SFcnParamSettings.TxMode==3
	%assign bufferIndex=SFcnParamSettings.Buffers[0] - 1
	%assign client = "CAN node %<moduleLetter>"
	/* Send message using FIFO queue and dedicated hardware buffer; note
	* that interrupts for %<client> must be disabled while the send 
	* function is called */
	%<SLibC166TimersRaiseCPUPriority(client,SFcnParamSettings.IntLevel)>
	can_%<module>_send_can_message_fifo(%<LibBlockInputSignalAddr(0, "", lcv, idx)>,
	%<LibBlockPWork(pCanTxCircBuffer,"","",0)>, 
	%<CAST("Unsigned", bufferIndex)>);	
	%<SLibC166TimersLowerCPULevel()> 	
      %endif
    %endroll
    
  %endif
  
%endfunction %%Outputs

