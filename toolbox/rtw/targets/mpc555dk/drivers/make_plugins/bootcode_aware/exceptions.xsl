<?xml version="1.0" encoding="ISO-8859-1"?>
<!--
File : exceptions.xsl

Abstract:
	XSL Style sheet for generating the application
	exception table.

$Revision: 1.1.6.3 $
$Date: 2004/04/19 01:24:18 $ 

Copyright 2002-2004 The MathWorks, Inc.

+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ 

Usage :

saxon -o application_exceptions.s exception_defaults.xml exceptions.xsl mergedoc=<full-path>/myexceptions.xml 

	Details :

	exception_defaults.xml :
	This is the list ( in order ) of exceptions that can get passed to
	the application. An exception table must be built in the code 
	section ".application_bios"

	exceptions.xsl :
	This is the transform that builds the exception table.

	myexceptions.xml :
	A file that is particular to an application. For example

	<exceptions>
		<register exception="external_interrupt_exception" 		handler="isr_handler"/>
	</exceptions>

	See exception_defaults.xml for the exceptions you can override. The handler
	is an external function you wish to be the handler.

	will be the typical contents of this file indicating the
	exceptions that you are interested in dealing with. You have to
	give the full path to this file using URL notation. eg

	mergedoc=file:/d:/temp/myexceptions.xml

	application_exceptions.s :
	The output file which needs to be built and linked into the application.
	An entry in the linker command file for section .application_bios
	located at 0x3f9800 needs to be created.

	@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
-->
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

	<xsl:output method="xml" indent="no" omit-xml-declaration="yes"/>
	<xsl:strip-space elements="*"/>
	<xsl:param name="mergedoc" select="''"/>    <!--source of data-->

	<!--
	* Match the root document
	* 
	* Do not change the indenting
	* -->
<xsl:template match="/">        <!--document element-->
; -------------------------------------------------------------------
; This file is automatically generated by exceptions.xsl Do not edit
;
; See the bootcode_aware module of the mpc555 for details.
; -------------------------------------------------------------------

;-----------------------------------------------------------
; The __start location is the entry point to the application
;-----------------------------------------------------------
	.import __start

;-----------------------------------------------------------
; To handle the decrementer exception
;-----------------------------------------------------------
	.import decrementer_isr

; The default handler just loops
;-----------------------------------------------
	.text
default:
	ba default

	;--------------------------------------------
	; An entry in the application linker command
	; file is required to support this module
	;--------------------------------------------
	.section .application_bios
	<xsl:call-template name="export"/>

	<xsl:call-template name="import"/>

	<xsl:call-template name="table"/>
	
</xsl:template>

<!-- 
* Match a register
*
* Do not change the indenting
* -->
<xsl:template name="table" >
	<xsl:for-each select="exceptions/register">
;------------------------------------------------
<xsl:value-of select="@exception"/>:
		<xsl:variable name="id" select="string(@exception)"/>	
		<xsl:variable name="doc" select="document($mergedoc)"/>
		<xsl:variable name="handler">
			<!-- Check to see if there are any overloaded exceptions -->
			<xsl:choose > 
				<xsl:when test = "$doc/exceptions/register[@exception=$id]" > 
					<!-- The exception is overloaded -->
					<xsl:value-of select="$doc/exceptions/register[@exception=$id]/@handler"/>
				</xsl:when>
				<xsl:otherwise>
					<!-- Use the default exception -->
					<xsl:value-of select="@handler"/>
				</xsl:otherwise>
			</xsl:choose>
		</xsl:variable>
	ba <xsl:value-of select="$handler"/>
	</xsl:for-each>
</xsl:template>

<!-- Generates the export statement -->
<xsl:template name="export" >
	<xsl:for-each select="exceptions/register">
	.export <xsl:value-of select="@exception"/>
	</xsl:for-each>
</xsl:template>

<!-- Generates the export statement -->
<xsl:template name="import" >
	<xsl:variable name="doc" select="document($mergedoc)"/>
	<xsl:for-each select="$doc/exceptions/register">
	.extern <xsl:value-of select="@handler"/>
	</xsl:for-each>
</xsl:template>

</xsl:stylesheet>
