%%
%% $Revision: 1.1 $ 
%% $RCSfile: oseksetalarm.tlc,v $
%%
%% Abstract:
%%      TLC file for the OSEK Set Alarm Block.
%%
%% Copyright 2002 The MathWorks, Inc.

%implements "oseksetalarm" "C"

%include "osekblocklib.tlc"

%% Function: BlockTypeSetup ===================================================
%% Abstract:
%%      Add OSEK API headers for the selected implementation to the generated
%%      model.c file as this block will generate OSEK API calls in model.c.
%%
%function BlockTypeSetup(block, system) void
  %<OSLibIncludeImpHeaderInModel()>
%endfunction

%% Function: BlockInstanceSetup ===============================================
%% Abstract:
%%      Find the function-call subsystems that are attached to the task
%%      block and
%%
%%      o Add a Task to the OIL file definitions and generated C code
%%        The task is defined with the parameters specified in the block.
%%
%function BlockInstanceSetup(block, system) void
  
  %assign sysIdx = system.CallSites[0][2]
  %assign blkIdx = system.CallSites[0][3]
  %assign taskBlock = CompiledModel.System[sysIdx].Block[blkIdx]
  %assign sourceType = IDNUM(taskBlock.ControlInputPort.SignalSrc[0])
  
  %% Determine masked block name for error reporting
  %assign sysId = %<SYSNAME(taskBlock.Name)>[0]
  %assign idIdx = %<IDNUM(sysId)>[1] - 1
  %assign parentName = CompiledModel.Subsystem[idIdx].Name
  %assign taskBlockName = STRING(LibUnmangledPathName(parentName))
  %assign block = block + taskBlockName

  %% The input to the task block is unconnected
  %if sourceType[0]  == "G"
    %assign wrnTxt = "The input for OSEK Task block '%<taskBlockName>' is " ...
      "unconnected."
    %warning %<wrnTxt>
  %endif
  
  %% Get downstream block if there is one
  %if "%<SFcnSystemOutputCall[0].BlockToCall>" == "unconnected"
    %assign wrnTxt = "The output for OSEK Alarm block '%<taskBlockName>' is " ...
      "unconnected.  No code will be generated for this block."
    %warning %<wrnTxt>
  %else
    %% Find the downstream function-call subsystem.
    %% Simulink will have already verified that, if connected at all,  it is 
    %% a function-call subsystem and that it is the only destination.  The 
    %% remaining test is whether the destination has other function-call 
    %% sources besides this Task block which is not allowed since the Task block
    %% modifies the code of the function-call subsystem.
    %% That test can done by checking ssBlock.ControlInputPort.Width.
    
    %% Get downstream block using SFcnSystemOutputCall.BlockToCall
    %assign ssSysIdx = SFcnSystemOutputCall[0].BlockToCall[0]
    %assign ssBlkIdx = SFcnSystemOutputCall[0].BlockToCall[1]
    %assign ssBlock = CompiledModel.System[ssSysIdx].Block[ssBlkIdx]
    %assign block = block + ssBlock
    %% Check to see if this is a direct connection
    %if (ssBlock.ControlInputPort.Width != 1)
      %assign errTxt = "The OSEK Alarm block '%<taskBlockName>' " ...
	"output must be directly connected to one function-call subsystem. " ...
	"Also, no other block can drive the function-call subsystem."
      %exit RTW Fatal: %<errTxt>
    %endif    
    
    %% Assume it is a subsystem block(Simulink checked for a f-c subsys already).
    %assign taskSystem = System[ssBlock.ParamSettings.SystemIdx]
    %assign block = block + taskSystem
    
    %% Tell Task Block Alarm Block is Driving it
    %assign AlarmDriven = 1
    %assign taskSystem = taskSystem + AlarmDriven
  %endif
%endfunction
  
%function Start(block, system) Output
  %if EXISTS("ssBlock")
    %if SFcnParamSettings.CallAtStartup == "on"
      %if SFcnParamSettings.AlarmType == "Relative"
	  SetRelAlarm(%<SFcnParamSettings.AlarmName>,\
	  %<CAST("Number",SFcnParamSettings.Increment)>,\
	  %<CAST("Number",SFcnParamSettings.Cyclic)>);
      %else
	  SetAbsAlarm(%<SFcnParamSettings.AlarmName>,\
	  %<CAST("Number",SFcnParamSettings.Increment)>,\
	  %<CAST("Number",SFcnParamSettings.Cyclic)>);
      %endif
    %endif
  %endif
%endfunction

%function Outputs(block, system) Output
  %if EXISTS("ssBlock")
    %% Define the Alarm in the oil file
    %openfile buffer
    ALARM %<SFcnParamSettings.AlarmName> {
      COUNTER = SystemTimer;
      ACTION = ACTIVATETASK {
	TASK = %<taskSystem.TaskName>;
      };
    };
    %closefile buffer
    %<OSLibOSEKAddAlarmToOil(buffer)>
    %% Only generate Set Alarm if no for Startup.
    %if SFcnParamSettings.CallAtStartup == "off"
      %if SFcnParamSettings.AlarmType == "Relative"
	  SetRelAlarm(%<SFcnParamSettings.AlarmName>,\
	  %<CAST("Number",SFcnParamSettings.Increment)>, 0);
      %else
	  SetAbsAlarm(%<SFcnParamSettings.AlarmName>,\
	  %<CAST("Number",SFcnParamSettings.Increment)>, 0);
      %endif
    %endif
   
  %endif
%endfunction
	
%% [EOF] oseksetalarm.tlc
